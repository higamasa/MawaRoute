apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

def GNAVI_API_VERSION = "20150630"
def GNAVI_API_KEY

def RELEASE_STORE_PASSWORD
def RELEASE_KEY_ALIAS
def RELEASE_KEY_PASSWORD

def secretPropsFile = file("$rootDir/secret.properties")
if (secretPropsFile.canRead()) {
    Properties secretProps = new Properties()
    secretProps.load(new FileInputStream(secretPropsFile))
    RELEASE_STORE_PASSWORD = secretProps['RELEASE_STORE_PASSWORD']
    RELEASE_KEY_ALIAS = secretProps['RELEASE_KEY_ALIAS']
    RELEASE_KEY_PASSWORD = secretProps['RELEASE_KEY_PASSWORD']
    GNAVI_API_KEY = secretProps['GNAVI_API_KEY']
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.1'

    defaultConfig {
        applicationId "kies.mawaroute"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.1"
        multiDexEnabled true

        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "GNAVI_API_VERSION", "\"${GNAVI_API_VERSION}\""
        buildConfigField "String", "GNAVI_API_KEY", "\"${GNAVI_API_KEY}\""
    }
    signingConfigs {
        debug {
            storeFile file("$rootDir/app_debug.jks")
            storePassword 'android'
            keyAlias 'debug'
            keyPassword 'android'
        }
        release {
            storeFile file("$rootDir/app_release.jks")
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
            signingConfig signingConfigs.debug
        }
        release {
            shrinkResources true
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    dataBinding {
        enabled = true
    }
}

ext.support_version = '27.0.1'

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "com.android.support:appcompat-v7:${support_version}"
    implementation "com.android.support:design:${support_version}"
    implementation "com.android.support:cardview-v7:${support_version}"
    implementation "com.android.support:palette-v7:${support_version}"
    implementation "com.android.support:support-vector-drawable:${support_version}"
    implementation "com.android.support:animated-vector-drawable:${support_version}"
    implementation "com.android.support:support-v4:${support_version}"
    implementation 'com.android.support:multidex:1.0.2'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    kapt 'com.android.databinding:compiler:3.0.0'

    // Android Architecture Component
    implementation 'android.arch.lifecycle:runtime:1.0.3'
    implementation 'android.arch.lifecycle:extensions:1.0.0'
    kapt "android.arch.lifecycle:compiler:1.0.0"

    // Google Map
    implementation 'com.google.android.gms:play-services-location:11.6.0'
    implementation 'com.google.android.gms:play-services-maps:11.6.0'

    // Permission
    implementation('com.github.hotchemi:permissionsdispatcher:3.0.1') {
        exclude module: "support-v13"
    }
    kapt 'com.github.hotchemi:permissionsdispatcher-processor:3.0.1'

    // Reactive Stream
    implementation 'io.reactivex.rxjava2:rxjava:2.1.6'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'

    // Network
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation('com.squareup.retrofit2:converter-simplexml:2.3.0') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }

    // UI
    implementation 'com.squareup.picasso:picasso:2.5.2'

    // Firebase
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
    implementation 'com.google.firebase:firebase-core:11.6.0'

    // Test
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

apply plugin: 'com.google.gms.google-services'
